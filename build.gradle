buildscript {
    repositories {
        mavenCentral()
        maven {
            name = 'forge'
            url = 'https://maven.minecraftforge.net/'
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}
plugins {
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'se.bjurr.gitchangelog.git-changelog-gradle-plugin' version '1.71.4'
    id 'org.ajoberstar.grgit' version '4.1.1'
    id 'com.github.ben-manes.versions' version '0.39.0'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply from: 'https://raw.githubusercontent.com/SizableShrimp/Forge-Class-Remapper/main/classremapper.gradle'

group= 'cpw.mods'
archivesBaseName = 'inventorysorter'
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

//reckon {
//    scopeFromProp()
//    stageFromProp('ms', 'final')
//}
version = grgit.describe(longDescr: true).split('-').with { "${it[0]}.${it[1]}" }

minecraft {
    mappings channel: MCP_CHANNEL, version: MCP_MAPPINGS
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'info'
            property 'forge.logging.noansi', 'false'
            ideaModule "${project.name}.main"
        }

        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'info'
            property 'forge.logging.noansi', 'false'
            ideaModule "${project.name}.main"
        }
        
        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN'
            property 'forge.logging.console.level', 'debug'
            property 'forge.logging.noansi', 'false'
            ideaModule "${project.name}.main"
        }
    }
}


dependencies {
    minecraft "net.minecraftforge:forge:${MC_VERSION}-${FORGE_VERSION}"
}

task makeChangelog(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
    file = file('CHANGELOG.md')
    fromRepo = file(".")
    untaggedName = "Current release ${version}"
    fromRef = project.findProperty('changelogStart') ?: '13.0'
    templateContent = """
# Inventory sorter changelog history

## Version ${version} for Minecraft ${MC_VERSION}

{{#tags}}
* {{name}}
  {{#commits}}
  * {{{messageTitle}}}
  {{/commits}}
{{/tags}}
"""
}

curseforge {
    apiKey = project.hasProperty('curseforge_apikey') ? project.curseforge_apikey : '0'
    project {
        id = '240633'
        changelog = file('CHANGELOG.md')
        changelogType = 'markdown'
        releaseType = project.findProperty('releaseType') ?: 'alpha'
        addGameVersion '1.18'
    }
    options {
        javaVersionAutoDetect = true // defaults to true
        javaIntegration = true // defaults to true
        forgeGradleIntegration = true // defaults to true
    }
}

beforeEvaluate {
    tasks.curseforge240633.dependsOn.remove(reobfJar)
    tasks.curseforge240633.dependsOn.remove(jar)
    tasks.curseforge240633.dependsOn.add(makeChangelog)
}

jar {
    manifest {
        attributes([
                "Specification-Title": 'inventorysorter',
                "Specification-Vendor": 'cpwmods',
                "Specification-Version": '1', // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" :'cpwmods',
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
    appendix MC_VERSION
}

def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
    type 'jar'
    builtBy 'reobfJar'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact reobfArtifact
        }
    }
    repositories {
        maven {
            credentials {
                username project.properties.cpwMavenUser?:'spam'
                password project.properties.cpwMavenPassword?:'bums'
            }
            url 'https://maven.minecraftforge.net/'
        }
    }
}
